project.ext {
	java_version	=	'1.7'
	version			=   '0.0.00'

	// projects
	util			= 	':sys02-util'
	model			= 	':sys02-model'
	service			= 	':sys02-service'
	web				= 	':sys02-web'

	// server
	servlet			=   'javax.servlet:javax.servlet-api:3.0.1'
	jetty			=	'org.mortbay.jetty:jetty-runner:8.1.0.RC0'

	// spring
	spring_webmvc			=	'org.springframework:spring-webmvc:4.0.0.RELEASE'
	spring_webflow			=	'org.springframework.webflow:spring-webflow:2.4.0.RELEASE'
	spring_faces			=	'org.springframework.webflow:spring-faces:2.4.0.RELEASE'
	spring_security 		= 	'org.springframework.security:spring-security-web:3.2.5.RELEASE'
	spring_security_config	=	'org.springframework.security:spring-security-config:3.2.5.RELEASE'
	spring_security_taglibs	=	'org.springframework.security:spring-security-taglibs:3.2.5.RELEASE'

	//jsf
	jsf_api			=	'com.sun.faces:jsf-api:2.2.0'
	jsf_impl		=   'com.sun.faces:jsf-impl:2.2.0'
	primerfaces		=   'org.primefaces:primefaces:4.0'

	// lang
	groovy 			=	'org.codehaus.groovy:groovy-all:2.3.6'
	scala			=	'org.scala-lang:scala-compiler:2.11.0'

	// util
	slf4j			=	'org.slf4j:slf4j-log4j12:1.7.5'
	jackson			=	'org.codehaus.jackson:jackson-mapper-asl:1.9.13'

	// test
	junit			=	'junit:junit:4.11'
}
def _=project.ext

allprojects {  version = _.version  }

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'

	sourceCompatibility = _.java_version

	repositories {
		mavenLocal()
		mavenCentral()
		maven{
			url 'http://repository.primefaces.org'
			url 'https://repo.spring.io/milestone'
		}
	}

	dependencies {
		compile 	_.slf4j
		testCompile _.junit
	}
}

/*
 * UTIL
 */
project(_.util){
}


/* 
 * MODEL
 */
project(_.model){

	apply plugin: 'scala'

	dependencies {
		compile project(_.util)
		compile _.scala
	}
}


/*
 * SERVICE
 */
project(_.service){

	apply plugin: 'groovy'

	dependencies {
		compile project (_.util)
		compile project (_.model)
		compile         _.groovy
	}
}


/*
 * WEB
 */
project(_.web){

	apply plugin: 'war'
	apply plugin: 'groovy'
	apply plugin: 'eclipse-wtp'

	eclipse {
		wtp {
			facet {
				facet name: 'java', version: _.java_version
				facet name: 'jst.web', version: '2.5'
				facet name: 'wst.jsdt.web', version: '1.0'
			}
		}
	}

	configurations { jetty8 }

	dependencies {
		compile project		(_.util)
		compile project 	(_.service)

		compile				_.spring_webmvc
		compile				_.spring_webflow
		compile				_.spring_faces
		compile				_.spring_security
		compile				_.spring_security_config
		compile				_.spring_security_taglibs
		compile				_.groovy
		compile 			_.jackson
		compile				_.jsf_api
		compile				_.jsf_impl
		compile				_.primerfaces

		providedCompile 	_.servlet

		jetty8			 	_.jetty
	}

	task jetty(type: JavaExec) {
		main = "org.mortbay.jetty.runner.Runner"
		args = [war.archivePath]
		classpath configurations.jetty8
	}
}

