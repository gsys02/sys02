project.ext {
	java_version	=	'1.7'
	version			=   '0.0.00'

	util			= 	':sys02-util'
	model			= 	':sys02-model'
	service			= 	':sys02-service'
	web				= 	':sys02-web'

	junit			=	'junit:junit:4.11'
	log4j			=	'org.slf4j:slf4j-log4j12:1.7.5'
	jackson			=	'org.codehaus.jackson:jackson-mapper-asl:1.9.13'
	spring_webmvc	=	'org.springframework:spring-webmvc:4.0.0.RELEASE'
	servlet			=   'javax.servlet:javax.servlet-api:3.0.1'
	groovy 			=	'org.codehaus.groovy:groovy-all:2.3.6'
	scala			=	'org.scala-lang:scala-compiler:2.11.0'
}
def _=project.ext

allprojects {  version = _.version  }

subprojects {
	apply plugin: 'java'
	apply plugin: 'eclipse'

	sourceCompatibility = _.java_version

	repositories {
		mavenLocal()
		mavenCentral()
	}
	dependencies {
		compile 	_.log4j
		testCompile _.junit
	}
}

/*
 * UTIL
 */
project(_.util){
}

/* 
 * MODEL
 */
project(_.model){

	apply plugin: 'scala'

	dependencies {
		compile project(_.util)
		compile _.scala
	}
}

/*
 * SERVICE
 */
project(_.service){

	apply plugin: 'groovy'

	dependencies {
		compile project (_.util)
		compile project (_.model)
		compile         _.groovy
	}
}

/*
 * WEB
 */
project(_.web){

	apply plugin: 'war'
	apply plugin: 'jetty'
	apply plugin: 'groovy'
	apply plugin: 'eclipse-wtp'

	dependencies {
		compile project (_.util)
		compile project (_.service)
		compile			 _.groovy

		compile 		 _.spring_webmvc
		compile 		 _.jackson

		providedCompile _.servlet
	}

	task jetty << {
		httpPort=8080
		jettyRunWar.execute()
	}
}

